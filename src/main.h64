## @module main
# Copyright (c) 2020-2024, ellie/@ell1e & Horp Team (see AUTHORS.md).
#
# Redistribution and use in source and binary forms, with or without
# modification, are permitted provided that the following conditions are met:
#
# 1. Redistributions of source code must retain the above copyright notice,
#    this list of conditions and the following disclaimer.
# 2. Redistributions in binary form must reproduce the above copyright notice,
#    this list of conditions and the following disclaimer in the documentation
#    and/or other materials provided with the distribution.
#
# THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
# AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
# IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
# ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT OWNER OR CONTRIBUTORS BE
# LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
# CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
# SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
# INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN
# CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE)
# ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE
# POSSIBILITY OF SUCH DAMAGE.
#
# Alternatively, at your option, this file is offered under the Apache 2
# license, see accompanied LICENSE.md.

import argparse from core.horse64.org
import system from core.horse64.org

import build.template
import package
import package.ref as ref
import package.servermeta

var package_refs_explainer = "(e.g. a package name, "
    "or dir path to a package's local source directory)"

func print_info_list_files(refs, limit_to_kind=none,
        code_with_templates=no, verbose=no) {
    var queue = refs.copy()

    var next_ref = queue.pop()
    if verbose {
        print("horp: verbose: print_info_list_files(): "
            "Obtaining file list for \"" +
            next_ref.package_name + "\"...")
    }
    var result = ref.print_files_list_from_ref(
        next_ref, limit_to_kind=limit_to_kind,
        code_with_templates=code_with_templates,
        verbose=verbose) later:

    await files
    if not result {
        print("horp: error: Failed to obtain files for this ref, "
            "check if this package is actually accessible: " +
            next_ref.as_str())
        return later 1
    }
    if queue.len == 0 {
        return later 0
    }

    next_ref = queue.pop()
    if verbose {
        print("horp: verbose: print_info_list_files(): "
            "Obtaining file list for \"" +
            next_ref.package_name + "\"...")
    }
    result = ref.print_files_list_from_ref(
        next_ref, limit_to_kind=limit_to_kind,
        code_with_templates=code_with_templates,
        verbose=verbose
    ) later repeat
}

func actionparser_info(args, verbose=no) {
    var defs = argparse.ArgumentDefs(
        program_name="horp info", description="Show info about the given "
            "packages.")
    defs.add_arg("action", description="Specify an action to execute, "
        "available choices being: 1. \"general\" (show the general info), "
        "2. \"files\" (list all files known to belong to the package), "
        "3. \"code-files\" (list all code files of the package), "
        "4. \"code-files-no-templates\" (list all code files, but "
        "omit the code file templates).")
    defs.add_switch("--filter", takes_arg=yes, description="For "
        "the \"files\" action, filter for a specific type of file, "
        "for example \"documentation files\".")
    defs.add_arg(
        "package refs", description="Package refs " +
        package_refs_explainer + " to print "
        "information about, like version, source location, etc.")
    defs.add_switch("--verbose", description="Output extra information "
        "when assembling the info.")
    var result = argparse.parse(defs, args)
    if result.output != "" {
        print(result.output)
        return result.exit_code
    }
    if result.args["--verbose"] {
        verbose = yes
    }
    var target_refs = ref.parse_and_populate_package_refs(
        result.args["package refs"], verbose=verbose
    ) later:

    do {
        await target_refs
    } rescue any as err {
        print("horp: error: Failed to resolve package refs: " +
            err.msg)
        print("Did you specify existing horp packages?")
        return 1
    }

    # Helper functions to execute detailed info requests if any:
    func print_info_list_files_if_needed(refs, verbose=no) {
        var do_list_files = no
        var do_list_kind_of_file = none
        var do_exclude_templates_from_code= no
        if result.args["action"] == "files" {
            do_list_files = yes
            filter = result.args["--filter"]
            if filter != none and filter.len > 0 {
                do_list_kind_of_file = filter
            }
        } elseif result.args["action"] == "code-files" {
            do_list_files = yes
            do_list_kind_of_file = "all code files"
        } elseif result.args["action"] == "code-files-no-templates" {
            do_list_files = yes
            do_list_kind_of_file = "all code files"
            do_exclude_templates_from_code = yes
        }
        if not do_list_files {
            return later none
        }
        did_list_files = yes
        var exit_code = print_info_list_files(
            refs, limit_to_kind=do_list_kind_of_file,
            code_with_templates=
                not do_exclude_templates_from_code,
            verbose=verbose
        ) later:

        await exit_code
        return later exit_code
    }

    # Figure out what info we should actually provide:
    var result = print_info_list_files_if_needed(
        target_refs, verbose=verbose) later:

    await result
    if result != none {
        return result
    }
    if result.args["action"] != "general" {
        print("horp: error: Encountered unknown action: " +
            result.args["action"])
        print("Use horp info --help to show the usage.")
        return 1
    }

    # If we arrive here we weren't asked to do anything special,
    # so just print out the basic refs:
    print("Package refs: " + target_refs.as_str())
    return 0
}

func actionparser_add(args, verbose=no) {
    var defs = argparse.ArgumentDefs(program_name="horp add",
        description="Install the specified packages to the local "
        "horse_modules directory.")
    defs.add_arg(
        "package refs", description="Package refs " +
        package_refs_explainer + " to install locally.")
    defs.add_switch(
        "--update", aliases=["-u"], takes_arg=no,
        description="If the package is already installed but "
            "a newer version is available, upgrade it.")
    defs.add_switch("--verbose", description="Output extra information "
        "when doing things.")
    var result = argparse.parse(defs, args)
    if result.output != "" {
        print(result.output)
        return result.exit_code
    }
    if result.args["--verbose"] {
        verbose = yes
    }
    var target_refs = ref.parse_and_locate_package_refs(
        result.args["package refs"], verbose=verbose
    ) later:

    do {
        await target_refs
    } rescue any as err {
        print("horp: error: Failed to resolve package refs: " +
            err.msg)
        print("Did you specify existing horp packages?")
    }
    print("Package refs: " + target_refs.as_str())
}

func main {
    var defs = new argparse.ArgumentDefs(
        program_name="horp",
        description="Horp is the official Horse64 package manager. "
            "Use it to get and upgrade additional libraries and tools.")
    defs.add_arg("action", description="Pick an available actions: "
        "add, info, del. To see details for an action, use it with "
        "--help.\nE.g.: horp add --help")
    defs.add_catchall("action args", description="List of "
        "arguments for the specified action")
    defs.add_switch("--verbose", description="Output extra information "
        "when doing things.")
    var result = argparse.parse(defs)
    if result.output != "" {
        print(result.output)
        return result.exit_code
    }
    if result.args["action"] == "add" {
        var return_code = actionparser_add(
            result.args["action args"],
            verbose=result.args["--verbose"]) later:

        await return_code
        return return_code
    } elseif result.args["action"] == "info" {
        var return_code = actionparser_info(
            result.args["action args"],
            verbose=result.args["--verbose"]) later:

        await return_code
        return return_code
    } elseif result.args["action"] == "template" {
        var return_code = build.template.actionparser_template(
            result.args["action args"],
            verbose=result.args["--verbose"]) later:

        await return_code
        return return_code
    } else {
        var commonly_wrong = {
            "add"-> {"install"},
            "add --reinstall"-> {"reinstall"},
            "info"-> {"show"},
            "del"-> {"delete", "remove", "uninstall"},
        }
        print("horp: error: Unknown action: " + result.args["action"])
        print("Use horp --help to show usage.")
        for entry in commonly_wrong {
            var variants = commonly_wrong[entry]
            if variants.has(result.args["action"].lower()) or
                    entry.lower() == result.args["action"] {
                print('Did you maybe mean to use "horp ' + entry + '"?')
            }
        }
        return 1
    }
}

